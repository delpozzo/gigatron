_runtimePath_ "../../at67/gbas/runtime"
_codeRomType_ ROMv3
cls : mode 2

' ------------------
'   Card Suits
' ------------------
const CLUBS = 0
const DIAMONDS = 1
const HEARTS = 2
const SPADES = 3

' ------------------
'   Card States
' ------------------
const INDECK = 0
const DRAWN = 1
const HELD = 2
const DISCARDED = 3

' ------------------
'   Face Cards
' ------------------
const JACK = 11
const QUEEN = 12
const KING = 13
const ACE = 14

' ------------------
'   Rounds
' ------------------
const BET = 0
const DEAL = 1
const DRAW = 2

' ------------------
'   Winning Hands
' ------------------
const NONE = 0
const JACKSORBETTER = 1
const TWOPAIR = 2
const THREEOFAKIND = 3
const STRAIGHT = 4
const FLUSH = 5
const FULLHOUSE = 6
const FOUROFAKIND = 7
const STRAIGHTFLUSH = 9
const ROYALFLUSH = 10

' ------------------
'   Coordinates
' ------------------
const cardY = 61
const cardY2 = cardY + 45
const holdY = cardY - 10
const cardBorderY = cardY - 1
const cardBorderY2 = cardBorderY + 47
const cardValueY = cardY + 2
const cardSuitY = cardY + 17

' ------------------
'   Sprites
' ------------------
load sprite, ./graphics/club.tga, CLUBS
load sprite, ./graphics/diamond.tga, DIAMONDS
load sprite, ./graphics/heart.tga, HEARTS
load sprite, ./graphics/spade.tga, SPADES

' ------------------
'   Card Deck
' ------------------
dim cardValue(52)
dim cardSuit(52)
dim cardState(52)
dim hand(5)

' ------------------
'   Other Vars
' ------------------
balance = 100
currentRound = DEAL
selectedCard = 0
pickedCard = 0
tempCard = 0
tempValue1 = 0
tempValue2 = 0
tempValue3 = 0
straightFlag = 0
flushFlag = 0
winType = NONE
holdX = 0
inputDevice = 255

' ------------------
'   Game Setup
' ------------------
gosub initCards
gosub displayPayouts
gosub dealCards

gosub displayCards
gosub selectCard

' ------------------
'   Main Loop
' ------------------
loop:
    gosub handleInput

    goto &loop

' ------------------
'   Input
' ------------------
handleInput:
    inputDevice = get("BUTTON_STATE")

    if (inputDevice = 254) AND (currentRound = DEAL)
        selectedCard = selectedCard + 1
        if selectedCard &= 5
            selectedCard = 0
        endif
        gosub selectCard
        wait 10
    endif

    if (inputDevice = 253) AND (currentRound = DEAL)
        selectedCard = selectedCard - 1
        if selectedCard &= -1
            selectedCard = 4
        endif
        gosub selectCard
        wait 10
    endif

    if inputDevice &= 191
        if currentRound &= DEAL
            currentRound = DRAW
            selectedCard = -1
            gosub selectCard
            gosub drawCards
            gosub displayCards
            gosub checkHand
        else
            currentRound = DEAL
            gosub clearHand
            gosub dealCards
            gosub displayCards
            gosub selectCard
        endif
        wait 10
    endif

    if (inputDevice = 127) AND (currentRound = DEAL)
        gosub holdCard
        wait 10
    endif
return

' ------------------
'   Init Cards
' ------------------
initCards:
    suit = 0

    for i = 0 to 51
        cardSuit(i) = suit
        cardValue(i) = 2+(i%13)
        cardState(i) = INDECK

        if(2+(i%13) &= 14)
            suit = suit + 1
        endif
    next i
return

' ------------------
'   Clear Hand
' ------------------
clearHand:
    for i = 0 to 51
        cardState(i) = INDECK
    next i

    set FG_COLOUR, 32
    set BG_COLOUR, 32
    selectedCard = 0
    at 7,holdY : print "                         "
    at 0,110 : print "                         "
return

' ------------------
'   Deal Cards
' ------------------
dealCards:
    for i = 0 to 4
retry1: pickedCard = rnd(52)
        if(cardState(pickedCard) &= INDECK)
            hand(i) = pickedCard
            cardState(pickedCard) = DRAWN
        else
            goto retry1
        endif
    next i

    selectedCard = 0
return

' For testing win conditions only:
cheatDeal:
    hand(0) = 4
    cardState(4) = DRAWN

    hand(1) = 2
    cardState(2) = DRAWN

    hand(2) = 31
    cardState(31) = DRAWN

    hand(3) = 18
    cardState(18) = DRAWN

    hand(4) = 5
    cardState(5) = DRAWN

    selectedCard = 0
return

' ------------------
'   Draw Cards
' ------------------
drawCards:
    for i = 0 to 4
        tempCard = hand(i)
        if(cardState(tempCard) &= DRAWN)
retry2:     pickedCard = rnd(52)
            if(cardState(pickedCard) &= INDECK)
                cardState(tempCard) = DISCARDED
                hand(i) = pickedCard
                cardState(pickedCard) = DRAWN
            else
                goto retry2
            endif
        endif
    next i
return

' ------------------
'   Select Card
' ------------------
selectCard:
    set FG_COLOUR, 0
    rect 3, cardBorderY, 32, cardBorderY2
    rect 34, cardBorderY, 63, cardBorderY2
    rect 65, cardBorderY, 94, cardBorderY2
    rect 96, cardBorderY, 125, cardBorderY2
    rect 127, cardBorderY, 156, cardBorderY2

    set FG_COLOUR, 3

    if selectedCard &= 0
        rect 3, cardBorderY, 32, cardBorderY2
    elseif selectedCard &= 1
        rect 34, cardBorderY, 63, cardBorderY2
    elseif selectedCard &= 2
        rect 65, cardBorderY, 94, cardBorderY2
    elseif selectedCard &= 3
        rect 96, cardBorderY, 125, cardBorderY2
    elseif selectedCard &= 4
        rect 127, cardBorderY, 156, cardBorderY2
    endif
return

' ------------------
'   Hold Card
' ------------------
holdCard:
    set FG_COLOUR, 31
    set BG_COLOUR, 32

    if selectedCard &= 0
        holdX = 7
    elseif selectedCard &= 1
        holdX = 38
    elseif selectedCard &= 2
        holdX = 69
    elseif selectedCard &= 3
        holdX = 100
    elseif selectedCard &= 4
        holdX = 131
    endif

    tempCard = hand(selectedCard)

    if cardState(tempCard) &= DRAWN
        cardState(tempCard) = HELD
        at holdX,holdY : print "HELD"
    elseif cardState(tempCard) &= HELD
        cardState(tempCard) = DRAWN
        at holdX,holdY : print "    "
    endif
return

' ------------------
'   Display Payouts
' ------------------
displayPayouts:
    set BG_COLOUR, 16
    set FG_COLOUR, 16
    rectf 3, 2, 156, 49
    set FG_COLOUR, 63
    rect 3, 2, 156, 49

    set FG_COLOUR, 15
    at 32,4 :  print "Royal Flush x250"
    set FG_COLOUR, 63
    at 5,13 : print "S.Flush  x50"
    at 5,22 : print "4-Kind   x25"
    at 5,31 : print "F.House  x9"
    at 5,40 : print "Flush    x6"
    at 90,13 : print "Straight x4"
    at 90,22 : print "3-Kind   x3"
    at 90,31 : print "Two Pair x2"
    at 90,40 : print "Jacks+   x1"
return

' ------------------
'   Display Cards
' ------------------
displayCards:
    set BG_COLOUR, 63

' Card 1
    if cardState(hand(0)) &= DRAWN
        set FG_COLOUR, 0
        rectf 3, cardBorderY, 32, cardBorderY2
        WAIT 10
        set FG_COLOUR, 63
        rectf 4, cardY, 31, cardY2
        if (cardSuit(hand(0)) = HEARTS) OR (cardSuit(hand(0)) = DIAMONDS)
            set FG_COLOUR, 3
        else
            set FG_COLOUR, 0
        endif
        if cardValue(hand(0)) &= 14
            at 7,cardValueY : print "A"
        elseif cardValue(hand(0)) &= 13
            at 7,cardValueY : print "K"
        elseif cardValue(hand(0)) &= 12
            at 7,cardValueY : print "Q"
        elseif cardValue(hand(0)) &= 11
            at 7,cardValueY : print "J"
        else
            at 7,cardValueY : print cardValue(hand(0))
        endif
        sprite noFlip, cardSuit(hand(0)), 11, cardSuitY
    endif

' Card 2
    if cardState(hand(1)) &= DRAWN
        set FG_COLOUR, 0
        rectf 34, cardBorderY, 63, cardBorderY2
        WAIT 10
        set FG_COLOUR, 63
        rectf 35, cardY, 62, cardY2
        if (cardSuit(hand(1)) = HEARTS) OR (cardSuit(hand(1)) = DIAMONDS)
            set FG_COLOUR, 3
        else
            set FG_COLOUR, 0
        endif
        if cardValue(hand(1)) &= 14
            at 38,cardValueY : print "A"
        elseif cardValue(hand(1)) &= 13
            at 38,cardValueY : print "K"
        elseif cardValue(hand(1)) &= 12
            at 38,cardValueY : print "Q"
        elseif cardValue(hand(1)) &= 11
            at 38,cardValueY : print "J"
        else
            at 38,cardValueY : print cardValue(hand(1))
        endif
        sprite noFlip, cardSuit(hand(1)), 42, cardSuitY
    endif

' Card 3
    if cardState(hand(2)) &= DRAWN
        set FG_COLOUR, 0
        rectf 65, cardBorderY, 94, cardBorderY2
        WAIT 10
        set FG_COLOUR, 63
        rectf 66, cardY, 93, cardY2
        if (cardSuit(hand(2)) = HEARTS) OR (cardSuit(hand(2)) = DIAMONDS)
            set FG_COLOUR, 3
        else
            set FG_COLOUR, 0
        endif
        if cardValue(hand(2)) &= 14
            at 69,cardValueY : print "A"
        elseif cardValue(hand(2)) &= 13
            at 69,cardValueY : print "K"
        elseif cardValue(hand(2)) &= 12
            at 69,cardValueY : print "Q"
        elseif cardValue(hand(2)) &= 11
            at 69,cardValueY : print "J"
        else
            at 69,cardValueY : print cardValue(hand(2))
        endif
        sprite noFlip, cardSuit(hand(2)), 73, cardSuitY
    endif

' Card 4
    if cardState(hand(3)) &= DRAWN
        set FG_COLOUR, 0
        rectf 96, cardBorderY, 125, cardBorderY2
        WAIT 10
        set FG_COLOUR, 63
        rectf 97, cardY, 124, cardY2
        if (cardSuit(hand(3)) = HEARTS) OR (cardSuit(hand(3)) = DIAMONDS)
            set FG_COLOUR, 3
        else
            set FG_COLOUR, 0
        endif
        if cardValue(hand(3)) &= 14
            at 100,cardValueY : print "A"
        elseif cardValue(hand(3)) &= 13
            at 100,cardValueY : print "K"
        elseif cardValue(hand(3)) &= 12
            at 100,cardValueY : print "Q"
        elseif cardValue(hand(3)) &= 11
            at 100,cardValueY : print "J"
        else
            at 100,cardValueY : print cardValue(hand(3))
        endif
        sprite noFlip, cardSuit(hand(3)), 104, cardSuitY
    endif

' Card 5
    if cardState(hand(4)) &= DRAWN
        set FG_COLOUR, 0
        rectf 127, cardBorderY, 156, cardBorderY2
        WAIT 10
        set FG_COLOUR, 63
        rectf 128, cardY, 155, cardY2
        if (cardSuit(hand(4)) = HEARTS) OR (cardSuit(hand(4)) = DIAMONDS)
            set FG_COLOUR, 3
        else
            set FG_COLOUR, 0
        endif
        if cardValue(hand(4)) &= 14
            at 131,cardValueY : print "A"
        elseif cardValue(hand(4)) &= 13
            at 131,cardValueY : print "K"
        elseif cardValue(hand(4)) &= 12
            at 131,cardValueY : print "Q"
        elseif cardValue(hand(4)) &= 11
            at 131,cardValueY : print "J"
        else
            at 131,cardValueY : print cardValue(hand(4))
        endif
        sprite noFlip, cardSuit(hand(4)), 135, cardSuitY
    endif
return

' ------------------
'   Sort Cards
' ------------------
sortCards:
    for i = 0 to 3
        for j = (1 + i) to 4
            tempValue1 = hand(i)
            tempValue2 = hand(j)
            if cardValue(tempValue1) &> cardValue(tempValue2)
                tempCard = hand(i)
                hand(i) = hand(j)
                hand(j) = tempCard
            endif
        next j
    next i
return

' ------------------
'   Check Hand
' ------------------
checkHand:
' reset flags
    straightFlag = 0
    flushFlag = 0
    winType = NONE
    set FG_COLOUR, 31
    set BG_COLOUR, 32

' sort cards least to greatest
    gosub sortCards

' check straight and flush flags first
    gosub flush
    gosub straight

' Royal Flush
    gosub royalFlush
    if winType &= ROYALFLUSH
        at 10,110 : print "Royal Flush!"
        return
    endif

' Straight Flush
    gosub straightFlush
    if winType &= STRAIGHTFLUSH
        at 10,110 : print "Straight Flush!"
        return
    endif

' Four of a Kind
    gosub fourOfAKind
    if winType &= FOUROFAKIND
        at 10,110 : print "Four of a Kind!"
        return
    endif

' Full House
    gosub fullHouse
    if winType &= FULLHOUSE
        at 10,110 : print "Full House!"
        return
    endif

' Flush
    if flushFlag &= 1
        at 10,110 : print "Flush!"
        return
    endif

' Straight
    if straightFlag &= 1
        at 10,110 : print "Straight!"
        return
    endif

' Three of a Kind
    gosub threeOfAKind
    if winType &= THREEOFAKIND
        at 10,110 : print "Three of a Kind!"
        return
    endif

' Two Pair
    gosub twoPair
    if winType &= TWOPAIR
        at 10,110 : print "Two Pair!"
        return
    endif

' Jacks or Better
    gosub jacksOrBetter
    if winType &= JACKSORBETTER
        at 10,110 : print "Jacks or Better!"
        return
    endif
return

' ------------------
'   Win Conditions
' ------------------
jacksOrBetter:
    for i = 0 to 4
        tempValue1 = hand(i)
        tempValue2 = hand(i+1)
        if (cardValue(tempValue1) = cardValue(tempValue2)) AND (cardValue(tempValue2) > 10)
            winType = JACKSORBETTER
        endif
    next i
return

twoPair:
    if (cardValue(hand(0)) = cardValue(hand(1))) AND (cardValue(hand(2)) = cardValue(hand(3))) 
        winType = TWOPAIR
    elseif (cardValue(hand(1)) = cardValue(hand(2))) AND (cardValue(hand(3)) = cardValue(hand(4))) 
        winType = TWOPAIR
    elseif (cardValue(hand(0)) = cardValue(hand(1))) AND (cardValue(hand(3)) = cardValue(hand(4))) 
        winType = TWOPAIR
    endif
return

threeOfAKind:
    for i = 0 to 2
        tempValue1 = hand(i)
        tempValue2 = hand(i+1)
        tempValue3 = hand(i+2)
        if (cardValue(tempValue1) = cardValue(tempValue2)) AND (cardValue(tempValue2) = cardValue(tempValue3))
            winType = THREEOFAKIND
        endif
    next i
return

straight:
    if (cardValue(hand(4)) = cardValue(hand(3))+1) AND (cardValue(hand(3))+1 = cardValue(hand(2))+2) AND (cardValue(hand(2))+2 = cardValue(hand(1))+3) AND (cardValue(hand(1))+3 = cardValue(hand(0))+4)
        straightFlag = 1
    elseif (cardValue(hand(4)) = ACE) AND (cardValue(hand(0)) = 2) AND (cardValue(hand(1)) = 3) AND (cardValue(hand(2)) = 4) AND (cardValue(hand(3)) = 5)
        straightFlag = 1
    endif
return

flush:
    if (cardSuit(hand(0)) = cardSuit(hand(1))) AND (cardSuit(hand(1)) = cardSuit(hand(2))) AND (cardSuit(hand(2)) = cardSuit(hand(3))) AND (cardSuit(hand(3)) = cardSuit(hand(4)))
        flushFlag = 1
    endif
return

fullHouse:
    if (cardValue(hand(0)) = cardValue(hand(1))) AND (cardValue(hand(1)) = cardValue(hand(2))) AND (cardValue(hand(3)) = cardValue(hand(4)))
        winType = FULLHOUSE
    elseif (cardValue(hand(0)) = cardValue(hand(1))) AND (cardValue(hand(2)) = cardValue(hand(3))) AND (cardValue(hand(3)) = cardValue(hand(4)))
        winType = FULLHOUSE
    endif
return

fourOfAKind:
    if (cardValue(hand(0)) = cardValue(hand(1))) AND (cardValue(hand(1)) = cardValue(hand(2))) AND (cardValue(hand(2)) = cardValue(hand(3)))
        winType = FOUROFAKIND
    elseif (cardValue(hand(1)) = cardValue(hand(2))) AND (cardValue(hand(2)) = cardValue(hand(3))) AND (cardValue(hand(3)) = cardValue(hand(4)))
        winType = FOUROFAKIND
    endif
return

straightFlush:
    if (straightFlag = 1) AND (flushFlag = 1)
        winType = STRAIGHTFLUSH
    endif
return

royalFlush:
    if (flushFlag = 1) AND (cardValue(hand(0)) = 10) AND (cardValue(hand(1)) = JACK) AND (cardValue(hand(2)) = QUEEN) AND (cardValue(hand(3)) = KING) AND (cardValue(hand(4)) = ACE)
        winType = ROYALFLUSH
    endif
return
