_runtimePath_ "../../at67/gbas/runtime"
_runtimeStart_ &h7FFF
_spriteStripeChunks_ 15, &h7000, descending
_codeRomType_ ROMv3
cls : mode 2

'audio fix for ROMv5a
poke &h21, peek(&h21) OR 3
sound off
cls

' ------------------
'   Constants
' ------------------
const MAXFISH = 8
const CARDINALTETRA = 0
const HARLEQUINRASBORA = 1
const SWIMRIGHT = 0
const SWIMLEFT = 1
const SWIMUP = 0
const SWIMDOWN = 1
const SWIMFWD = 2

' ------------------
'   Sprites
' ------------------
const TETRA1 = 0
load sprite, ./graphics/cardinalTetra1.tga, TETRA1
const TETRA2 = 1
load sprite, ./graphics/cardinalTetra2.tga, TETRA2
const RASBORA1 = 2
load sprite, ./graphics/harlequinRasbora1.tga, RASBORA1
const RASBORA2 = 3
load sprite, ./graphics/harlequinRasbora2.tga, RASBORA2

' ------------------
'   Fish Arrays
' ------------------
dim fishSprite%(MAXFISH)
dim fishType%(MAXFISH)
dim fishX%(MAXFISH)
dim fishY%(MAXFISH)
dim fishXmax%(MAXFISH)
dim fishXmin%(MAXFISH)
dim fishYmax%(MAXFISH)
dim fishYmin%(MAXFISH)
dim fishThink1%(MAXFISH)
dim fishThink2%(MAXFISH)
dim fishTimer%(MAXFISH)

' ------------------
'   Other Vars
' ------------------
inputDevice = 255

' ------------------
'   Game Setup
' ------------------
gosub initTank

' ------------------
'   Main Loop
' ------------------
loop:
    gosub updateFish
    gosub drawFish
    gosub checkInput
    goto &loop

' ------------------
'   Fish
' ------------------
updateFish:
    for i = 0 to MAXFISH
        fishTimer(i) = fishTimer(i) + 1

        if fishTimer(i) > 100
            fishTimer(i) = 0
            fishRandom = rnd(100)
            if fishRandom < 10
                fishThink2(i) = SWIMDOWN
            elseif fishRandom < 20
                fishThink2(i) = SWIMUP
            else
                fishThink2(i) = SWIMFWD
            endif
        endif

        if fishThink1(i) &= SWIMRIGHT
            if fishX(i) > fishXmax(i)
                fishSprite(i) = fishSprite(i) + 1
                fishThink1(i) = SWIMLEFT
            else
                fishX(i) = fishX(i) + 1
            endif
        elseif fishThink1(i) &= SWIMLEFT
            if fishX(i) < fishXmin(i)
                fishSprite(i) = fishSprite(i) - 1
                fishThink1(i) = SWIMRIGHT
            else
                fishX(i) = fishX(i) - 1
            endif
        endif

        if fishThink2(i) &= SWIMUP
            if fishY(i) < fishYmin(i)
                fishThink2(i) = SWIMDOWN
            else
                fishY(i) = fishY(i) - 1
            endif
        elseif fishThink2(i) &= SWIMDOWN
            if fishY(i) > fishYmax(i)
                fishThink2(i) = SWIMUP
            else
                fishY(i) = fishY(i) + 1
            endif
        endif
    next i

    wait 2
return

drawFish:
    for i = 0 to MAXFISH
        sprite noFlip, fishSprite(i), fishX(i), fishY(i)
    next i
return


' ------------------
'   Input
' ------------------
checkInput:
    inputDevice = get("BUTTON_STATE")

    if inputDevice = 127
        gosub initTank
    endif
return

' ------------------
'   Sound FX
' ------------------


' ------------------
'   Init
' ------------------
initTank:
    set BG_COLOUR, 32
    set FG_COLOUR, 32
    rectf 0, 0, 159, 119
    set FG_COLOUR, 47
    rectf 0, 105, 159, 119

    for i = 0 to MAXFISH
        fishType(i) = rnd(2)
        fishTimer(i) = rnd(100)

        if fishType(i) &= CARDINALTETRA
            fishXmax(i) = 140
            fishXmin(i) = 3
            fishYmax(i) = 90
            fishYmin(i) = 5
            fishX(i) = rnd(135)
            fishY(i) = rnd(90)
            fishThink1(i) = rnd(2)
            fishThink2(i) = SWIMFWD
            fishSprite(i) = TETRA1 + fishThink1(i)
        elseif fishType(i) &= HARLEQUINRASBORA
            fishXmax(i) = 135
            fishXmin(i) = 3
            fishYmax(i) = 90
            fishYmin(i) = 5
            fishX(i) = rnd(130)
            fishY(i) = rnd(90)
            fishThink1(i) = rnd(2)
            fishThink2(i) = SWIMFWD
            fishSprite(i) = RASBORA1 + fishThink1(i)
        endif
    next i
return

